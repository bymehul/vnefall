title "Inkframe v1.4.0: Visual + UI Demo"

# --- Setup ---
set player_name = "Zen"
set health = 100
set met_kaito = false

# --- The Start ---
scene "demo_game"
with fade 500
bg "room.png"

# Audio: music + ambience + sfx + voice
music "demo_theme.ogg"
ambience "demo_ambience.ogg"
sfx "ui_confirm.wav"
voice "demo_voice.wav"

volume master 0.8
save "auto_start"

with fade 250
char Emi show happy at center z 5
say Emi "Welcome to Inkframe v1.4.0!"
say Emi "UI now comes from ui.vnef, and my colors from char.vnef."
say Emi "We still support music, ambience, SFX, and voice."
say Emi "Inline tags: {color=0xFF66CCFF}color{/color} and {shake}shake{/shake}."
say Emi "Transitions are opt-in now: use 'with fade', 'with slide', or other types."
say Emi [speed=0.02] "This line types slower (per-line speed override)."

# Volume controls (0.0 - 1.0)
volume music 0.6
volume ambience 0.4
volume sfx 0.8
volume voice 0.9
say Emi "Volumes set. These persist in settings.vnef."

choice_add "Talk to Emi" talk_emi
choice_add "Go to the Lounge" go_lounge
choice_add "Transition Showcase" transition_showcase
choice_add "Logic + Variables" logic_test
choice_add "Test Audio Fades + Save" test_fades
choice_add "Test Stops (Silence)" test_stops
choice_add "Save Progress (v6)" save_game
choice_add "Clear Prefetch Cache" clear_prefetch
choice_add "Load Audio Save" load_audio
choice_show

label talk_emi:
    char Emi show normal at center z 5
    say Emi "I'm using the simplified sprite system."
    say Emi "Drop a PNG in my folder and call its name!"
    
    char Emi show happy at center z 5
    say Emi "I also scale to fit the screen. Pretty cool, right?"
    jump story_cont

label go_lounge:
    with wipe 450
    bg "lounge.jpg"
    char Emi show normal at left z 5
    say Emi "The lounge is quiet today."
    
    set met_kaito = true
    char Kaito show normal at right z 10
    say Kaito "Quiet? It was, until you brought ${player_name} here."
    
    char Kaito show angry at right z 10
    say Kaito "My Z is 10, hers is 5. So I overlap her."
    
    char Kaito hide
    say Emi "When we hide a character, the engine frees their memory."
    jump story_cont

label transition_showcase:
    say Emi "Quick tour of the new transitions."

    with slide 250
    bg "apartment.png"
    say Emi "Slide (good for entrances)."

    with dissolve 500
    bg "apartment_night.png"
    say Emi "Dissolve (soft cross-fade)."

    with zoom 300
    bg "bedroom.png"
    say Emi "Zoom (dramatic emphasis)."

    with blur 400
    bg "bedroom_night.png"
    say Emi "Blur (dreamy vibe)."

    with flash 120
    bg "night.png"
    say Emi "Flash (impact moments)."

    with shake 180
    bg "room.png"
    say Emi "Shake (camera-style impact)."

    say Emi "Characters can slide too:"
    with slide 250
    char Emi show happy at left z 5
    say Emi "Sliding in!"
    with slide 250
    char Emi hide

    say Emi "Shake also works on characters:"
    char Emi show normal at center z 5 with shake 200
    say Emi "Shaky entrance."
    char Emi hide with shake 200

    jump story_cont

label logic_test:
    say Emi "Let's test variables, math, and conditionals."
    set health = health + 5
    set nickname = "Star"
    say Emi "Health is now ${health}."
    say Emi "Nickname set to ${nickname}."

    if (health >= 105) {
        say Emi "Health check passed (>= 105)."
    } else {
        say Emi "Health check failed."
    }

    if (nickname == "Star") {
        say Emi "String comparison works too."
    }

    say Emi "I'll pause using the wait command."
    wait
    say Emi "Paused and resumed."

    # Legacy if-jump syntax
    if met_kaito jump kaito_note
    say Emi "Legacy if-jump skipped (you haven't met Kaito yet)."
    jump logic_done

label kaito_note:
    say Emi "Legacy if-jump triggered because you met Kaito."

label logic_done:
    jump story_cont

label test_fades:
    music_fade demo_theme_alt.ogg 1200
    ambience_fade demo_ambience_alt.ogg 1200
    sfx ui_confirm.wav
    voice demo_voice.wav
    say Emi "Faded audio tracks and triggered SFX/Voice."
    say Emi "I'm saving the current audio state now."
    save "audio_state"
    say Emi "Use 'Load Audio Save' to verify restore."
    jump story_cont

label test_stops:
    music_stop
    ambience_stop
    say Emi "Music and ambience stopped immediately."
    
    music "demo_theme.ogg"
    ambience "demo_ambience.ogg"
    say Emi "Restarted music and ambience."
    
    music_stop_fade 1200
    ambience_stop
    say Emi "Music faded out and ambience stopped."
    jump story_cont

label save_game:
    say Emi "Testing Sthiti-DB v6 persistence."
    save "demo_quicksave"
    say Emi "Audio + choices + variables are now saved!"
    jump story_cont

label clear_prefetch:
    scene_next "none"
    say Emi "Prefetch cache cleared manually."
    jump story_cont

label load_audio:
    load "audio_state"
    say Emi "If you see this, the save wasn't found."
    jump story_cont

label story_cont:
    with none
    bg "train.png"
    scene_next "demo_epilogue"
    
    char Emi show surprised at center z 5
    say Emi "We're nearing the end of this module."
    say Emi "scene_next also prefetches audio now."
    
    sfx ui_confirm.wav
    voice demo_voice.wav
    ambience "demo_ambience.ogg"
    
    if (met_kaito == true) {
        say Emi "Since you met Kaito, I should warn you... he's a bit grumpy."
    } else {
        say Emi "Since you avoided the lounge, it's been a peaceful day!"
    }
    
    say Emi "Ready for the next script?"
    
    choice_add "Jump to Epilogue" do_jump
    choice_add "Reload My Save" do_load
    choice_show

label do_jump:
    jump_file "demo_epilogue.vnef"

label do_load:
    load "demo_quicksave"
    say Emi "Error: Save not found!"
    end
